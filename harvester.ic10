push HASH("D1")
push 1
push HASH("D2")
push 2
push HASH("D3")
push 3
push HASH("D4")
push 4
push HASH("D5")
push 5
push HASH("D6")
push 6
push HASH("D7")
push 7
push HASH("D8")
push 8
push HASH("D11")
push 11
push HASH("D12")
push 12
push HASH("D13")
push 13
push HASH("D14")
push 14

alias dArm d0
alias gasSensor d1
alias Led1 d2

alias rNameHash r0
alias rTargetIndex r1
alias rArmIdle r2
alias rTakeFromIndex r3
alias rPutAtIndex r4
alias rStackEnd r5

alias rTemp1 r6
alias rTemp2 r7
alias rTemp3 r8

alias currentTemp r9
alias error r10

define trayHash HASH("StructureHydroponicsTrayData")
define binHash HASH("StructureChuteBin")
define compHash HASH("StructureAdvancedComposter")

define pickupIndex 10
define pickupFertilizerIndex 0
define dropIndex 9
define stackStart 0
define stackEnd 23
define setpoint 303.15

add sp stackStart 1
add rStackEnd stackEnd 2

loop:
l currentTemp gasSensor Temperature
sub r15 currentTemp setpoint
s db Setting r15
sub r14 currentTemp 273.15
s Led1 Setting r14

lbs r13 binHash 0 Occupied 0
breq r13 0 2
sb binHash Open 0

lb r13 compHash Quantity 0
brlt r13 5 2
sb compHash Activate 1

peek rNameHash
add sp sp 1
peek rTargetIndex
add sp sp 1
brle sp rStackEnd 3
move sp 1
j loop
lbns rTemp1 trayHash rNameHash 0 Occupied 1
beq rTemp1 0 plant
lbns rTemp1 trayHash rNameHash 0 Mature 1
lbns rTemp2 trayHash rNameHash 0 Seeding 1
add rTemp3 rTemp1 rTemp2
bgt rTemp3 0 harvest
lbns rTemp1 trayHash rNameHash 1 Occupied 1
beq rTemp1 0 fertilize
j loop

fertilize:
move rTakeFromIndex pickupFertilizerIndex
move rPutAtIndex rTargetIndex
j takeAndPut

harvest:
move rTakeFromIndex rTargetIndex
move rPutAtIndex dropIndex
j takeAndPut

plant:
move rTakeFromIndex pickupIndex
move rPutAtIndex rTargetIndex
j takeAndPut

takeAndPut:
s dArm Setting rTakeFromIndex
jal wait
s dArm Activate 1
jal wait
s dArm Setting rPutAtIndex
jal wait
s dArm Activate 1
jal wait
j loop

wait:
yield
l rArmIdle dArm Idle
breq rArmIdle 0 -2
j ra